apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.neenbedankt.android-apt' //needed for source code generation

android {

    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "org.stepic.droid"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 55
        versionName "1.9"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // Enabling multidex support.
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "DEV"
        }
    }

    productFlavors {
        sb {
            applicationId "org.stepic.droid.sb"
        }
        classic {
            applicationId "org.stepic.droid"
        }
        releasedev{
            applicationId "org.stepic.droid.release"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'mips',  'x86' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }

    // map for the version code
    project.ext.versionCodes = ['armeabi-v7a': 2,  'mips': 5, 'x86': 8]

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) + android.defaultConfig.versionCode*10
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {

    //<DI>
    apt 'com.google.dagger:dagger-compiler:2.0.1' //needed for source code generation
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.dagger:dagger:2.0.1' //dagger itself
    provided 'org.glassfish:javax.annotation:10.0-b28'
    //needed to resolve compilation errors, thanks to tutplus.org for finding the dependency
    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'javax.annotation:jsr250-api:1.0'
    //</DI>

    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.code.gson:gson:2.4'

    compile 'com.squareup.okio:okio:1.6.0'

    compile 'de.hdodenhof:circleimageview:1.3.0'

    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:palette-v7:23.3.0'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.facebook.fresco:fresco:0.9.0'

    compile 'joda-time:joda-time:2.8'

    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'

    compile 'com.squareup:otto:1.3.8'

    // AppMetrica from maven central. Protobuf will be included as dependency.
    compile 'com.yandex.android:mobmetricalib:2.21@aar'

    compile 'jp.wasabeef:recyclerview-animators:2.1.0'

    compile 'uk.co.chrisjenx:calligraphy:2.1.0'

    compile 'me.zhanghai.android.materialprogressbar:library:1.1.6'

    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito frameworkвв ьфермшуц фы вузутвутсн
    testCompile 'org.mockito:mockito-core:1.10.19'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2' // or 1.3.1
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2' // or 1.3.1
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2' // or 1.3.1

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"


    compile files('libs/commons-logging-api-1.1.1.jar')
    compile files('libs/jericho-html-3.4.jar')
    compile files('libs/log4j-api-2.4.1.jar')
    compile files('libs/slf4j-api-1.7.12.jar')

    compile 'de.mrmaffen:vlc-android-sdk:1.9.0'

    compile "com.google.android.gms:play-services-gcm:8.4.0"
}

repositories {
    mavenCentral()
}
buildscript {
    ext.kotlin_version = '1.0.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

}
apply plugin: 'com.google.gms.google-services'