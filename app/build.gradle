apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply from: '../code_quality_tools/jacoco.gradle'
apply from: '../code_quality_tools/findbugs.gradle'
apply from: '../code_quality_tools/checkstyle.gradle'
apply from: '../code_quality_tools/pmd.gradle'

android {

    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    signingConfigs {
        debug {
            storeFile file(debugKeystore)
            storePassword debugKeystorePass
            keyAlias debugAlias
            keyPassword debugAliasPass
        }

        release {
            storeFile file(keystore)
            storePassword keystorePass
            keyAlias alias
            keyPassword aliasPass
        }

        oldRelease {
            storeFile file(keystore)
            storePassword keystorePass
            keyAlias oldAlias
            keyPassword oldAliasPass
        }
    }

    defaultConfig {
        applicationId "org.stepic.droid"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode 197
        versionName "1.47"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride = android.defaultConfig.versionCode * 10
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        warningsAsErrors false // after fixing existing errors set to true for fixing warnings
        abortOnError false // after fixing existing errors set to true for failing fast
        lintConfig rootProject.file('lint.xml')
        htmlOutput rootProject.file("reports/lint/lint.html")
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            useProguard true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'proguard-rules-dev.pro'
            versionNameSuffix "DEV"

            testCoverageEnabled = false
        }

        //the apk of this type is signed by old prod keys
        //it makes apk testable (sign in, smart lock, etc are worked).
        //now new sign configs are used for providing to Google Play
        //assemble with oldProd on release/* branches
        oldRelease {
            initWith release
            signingConfig signingConfigs.oldRelease
            matchingFallbacks = ['release']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

kapt {
    generateStubs = true
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation project(':androidsvg')

    //<DI>
    compile 'com.google.dagger:dagger:2.10'
    kapt 'com.google.dagger:dagger-compiler:2.10'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    //needed to resolve compilation errors, thanks to tutplus.org for finding the dependency

    compile 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    //</DI>

    compile 'com.google.code.gson:gson:2.7'

    compile 'de.hdodenhof:circleimageview:2.1.0'

    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'

    compile "com.squareup.retrofit2:retrofit:${rootProject.retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${rootProject.retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava2:${rootProject.retrofitVersion}"

    // AppMetrica from maven central. Protobuf will be included as dependency.
    compile 'com.yandex.android:mobmetricalib:2.62'

    compile 'jp.wasabeef:recyclerview-animators:2.1.0'

    compile 'uk.co.chrisjenx:calligraphy:2.1.0'

    compile 'me.zhanghai.android.materialprogressbar:library:1.1.6'

    compile 'org.jsoup:jsoup:1.8.3'

    compile "com.google.firebase:firebase-messaging:${rootProject.firebaseVersion}"
    compile "com.google.firebase:firebase-appindexing:${rootProject.firebaseVersion}"
    compile "com.google.android.gms:play-services-auth:${rootProject.firebaseVersion}"
    compile "com.google.firebase:firebase-core:${rootProject.firebaseVersion}"
    compile "com.google.firebase:firebase-crash:${rootProject.firebaseVersion}"
    compile "com.google.firebase:firebase-config:${rootProject.firebaseVersion}"

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.facebook.android:facebook-android-sdk:4.17.0'
    compile 'com.vk:androidsdk:1.6.5'
    compile 'com.jakewharton.timber:timber:4.5.1'

    compile 'com.github.javiersantos:MaterialStyledDialogs:2.0'
    compile 'biz.kasual:materialnumberpicker:1.2.1'

    compile 'com.github.chrisbanes:PhotoView:1.3.1'


    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar')
    //depends on (remove this library in future or be care, when update support libs)
    //    |    +--- com.android.support:appcompat-v7:27.0.0 (*)
    //    |    \--- com.android.support:recyclerview-v7:27.0.0 (*)

    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile 'com.facebook.stetho:stetho:1.4.2'
    compile 'com.facebook.stetho:stetho-okhttp3:1.4.2'

    //add exo player for video (change playback speed supported)
    compile 'com.google.android.exoplayer:exoplayer-core:r2.5.3'

    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.5'

    debugCompile "com.squareup.leakcanary:leakcanary-android:${rootProject.leakCanaryVersion}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${rootProject.leakCanaryVersion}"

    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito framework
    testCompile 'org.mockito:mockito-core:2.7.21'
    testCompile 'org.robolectric:robolectric:3.1.2'
    testCompile 'joda-time:joda-time:2.8'

    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'

    androidTestCompile 'com.android.support:support-annotations:26.1.0'
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2') {
        exclude group: 'com.android.support'
    }
}

apply plugin: 'com.google.gms.google-services'
